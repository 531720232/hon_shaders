// (C)2009 S2 Games
// mesh_color_andromeda.psh
// 
// ...
//=============================================================================

//=============================================================================
// Headers
//=============================================================================
#include "../common/common.h"
#include "../common/fog.h"

//=============================================================================
// Global variables
//=============================================================================
float3		vSunPositionWorld;
float3		vAmbient;
float3		vSunColor;
float3		vCameraRight;
float3x3	mWorldRotate;

#ifdef GROUND_AMBIENT
float3		vViewUp;
#endif

float2		vSpec;

#if (NUM_POINT_LIGHTS > 0)
float3		vPointLightPositionOffset[NUM_POINT_LIGHTS];
float3		vPointLightColor[NUM_POINT_LIGHTS];
float2		vPointLightFalloff[NUM_POINT_LIGHTS];
#endif

float		fDistance;

float2		vBGScale;
float2		vBGOffset;
float		fDistanceScale;
float3		vBGColor;

float		fReflect;
float3		vReflectColor;
float3 		vReflectFresnel; // Bias, Scale, Power

//=============================================================================
// Vertex shader output structure
//=============================================================================
struct VS_OUTPUT
{
	float4 Color0 : COLOR0;
	float2 Texcoord0 : TEXCOORDX;
		#include "../common/inc_texcoord.h"
	float4 PositionProj : TEXCOORDX;
		#include "../common/inc_texcoord.h"
#if (LIGHTING_QUALITY == 0 || LIGHTING_QUALITY == 1 || FALLOFF_QUALITY == 0)
	float3 PositionOffset : TEXCOORDX;
		#include "../common/inc_texcoord.h"
#endif
#if (LIGHTING_QUALITY == 0 || LIGHTING_QUALITY == 1)
	float3 Normal : TEXCOORDX;
		#include "../common/inc_texcoord.h"
	float3 Tangent : TEXCOORDX;
		#include "../common/inc_texcoord.h"
	float3 Binormal : TEXCOORDX;
		#include "../common/inc_texcoord.h"
#elif (LIGHTING_QUALITY == 2)
	float3 Reflect : TEXCOORDX;
		#include "../common/inc_texcoord.h"
	float3 DiffLight : TEXCOORDX;
		#include "../common/inc_texcoord.h"
	float3 Refract : TEXCOORDX;
		#include "../common/inc_texcoord.h"
#endif
#ifdef CLOUDS
	float2 TexcoordClouds : TEXCOORDX;
		#include "../common/inc_texcoord.h"
#endif
#if ((FOG_QUALITY == 1 && FOG_TYPE != 0) || FALLOFF_QUALITY == 1 || FOG_OF_WAR == 1)
	float4 Last : TEXCOORDX;
		#include "../common/inc_texcoord.h"
#endif
};

//=============================================================================
// Pixel shader input structure
//=============================================================================
struct PS_OUTPUT
{
	float4 Color : COLOR;
};

sampler diffuse;

#ifndef RXGB_NORMALMAP
sampler normalmap;
#else
sampler normalmap_0;
#endif

samplerCUBE cube;
sampler specular;
//sampler team;
sampler background;

#ifdef CLOUDS
sampler clouds;
#endif

#if (FOG_OF_WAR == 1)
sampler fogofwar;
#endif

//=============================================================================
// Pixel Shader
//=============================================================================
PS_OUTPUT PS( VS_OUTPUT In )
{
	PS_OUTPUT Out;
	
	float4 cDiffuseColor = tex2D(diffuse, In.Texcoord0);
	
#if (TEXKILL == 1)
	clip(cDiffuseColor.a - 91.0f / 255.0f);
#endif

	//float fTeamMask = tex2D(team, In.Texcoord0).a;

	cDiffuseColor.rgb *= In.Color0.rgb;//lerp(In.Color0.rgb, float3(1.0f, 1.0f, 1.0f), fTeamMask);
	cDiffuseColor.a *= In.Color0.a;
	
	//cDiffuseColor.rgb *= lerp(float3(1.0f, 1.0f, 1.0f), vTeamColor, fTeamMask);
	
#if (LIGHTING_QUALITY == 0 || LIGHTING_QUALITY == 1)
	float3 vCamDirection = -normalize(In.PositionOffset);
#endif

#if (FOG_TYPE != 0)
	#if (FALLOFF_QUALITY == 1)
	float fCamDistance = In.Last.z;
	#else
	float fCamDistance = length(In.PositionOffset);
	#endif
#endif
		
	//
	// Lighting
	//	
	
#if (LIGHTING != 0)

	#ifndef RXGB_NORMALMAP
	float3 cNormalmapColor = tex2D(normalmap, In.Texcoord0).rgb;
	#else
	float3 cNormalmapColor = tex2D(normalmap_0, In.Texcoord0).agb;
	#endif
	
	float3 vTexNormal = float3(cNormalmapColor.rgb * 2.0f - 1.0f);
	
	float3 vDiffuse = vAmbient;
	float3 vSpecular = float3(0.0f, 0.0f, 0.0f);
	float fSpecular = vSpec.x;
	
	#if (LIGHTING_QUALITY == 0 || LIGHTING_QUALITY == 1)
	float3x3 mRotation = float3x3(In.Tangent, In.Binormal, In.Normal);
	float3 vNormal = normalize(mul(vTexNormal, mRotation));
	
		#ifdef GROUND_AMBIENT
	vDiffuse *= (dot(vNormal, vViewUp) * 0.375f + 0.625f);
		#endif
	
		#ifdef AMBIENT_OCCLUSION
	vDiffuse *= dot(vNormal, In.Normal);
		#endif
	#endif
	
	// Sun
	#if (LIGHTING_QUALITY == 2)

	vDiffuse += In.DiffLight.xyz;

	#else // 0 or 1

	float3 vLight = vSunPositionWorld;		
	float3 vHalfAngle = normalize(vLight + vCamDirection);
		
		#ifdef SMOOTH_SELF_OCCLUDE
	float fSelfOcclude = saturate(dot(In.Normal, vLight) * 5.0f);
		#else
	float fSelfOcclude = 1.0f;
		#endif
		
	float2 vLit; // x = diffuse, y = specular
		
	vLit.x = smoothstep(0.0f, 1.0f, dot(vNormal, vLight) * 1.3f);
	vLit.y = pow(saturate(dot(vNormal, vHalfAngle)), vSpec.y) * fSpecular;

	vLit *= fSelfOcclude;

		#ifdef CLOUDS
	float4 cCloudColor = tex2D(clouds, In.TexcoordClouds);

	vDiffuse += vSunColor * vLit.x * cCloudColor;
	vSpecular += vSunColor * vLit.y * cCloudColor;
		#else
	vDiffuse += vSunColor * vLit.x;
	vSpecular += vSunColor * vLit.y;
		#endif
	
		#if (NUM_POINT_LIGHTS > 0 && LIGHTING_QUALITY == 0)
	// Point Lights
	[unroll(NUM_POINT_LIGHTS)] for (int i = 0; i < NUM_POINT_LIGHTS; ++i)
	{
		float3 vDeltaPosition = vPointLightPositionOffset[i] - In.PositionOffset;
		float fDistance = length(vDeltaPosition);
	
		float3 vLight = normalize(vDeltaPosition);
		float3 vHalfAngle = normalize(vLight + vCamDirection);

		float fAttenuation = 1.0f - saturate(fDistance * vPointLightFalloff[i].x + vPointLightFalloff[i].y);
		
		float2 vLit; // x = diffuse, y = specular
		
		vLit.x = saturate(dot(vNormal, vLight));
		vLit.y = pow(saturate(dot(vNormal, vHalfAngle)), vSpec.y) * fSpecular;
		
		vLit *= fAttenuation;
		
		vDiffuse += vPointLightColor[i] * vLit.x;
		vSpecular += vPointLightColor[i] * vLit.y;
	}
		#endif
		
	//
	// Specular color
	//
	
	float4 vSpecColor = tex2D(specular, In.Texcoord0);
		
	vSpecular *= vSpecColor.rgb;

	#endif // LIGHTING_QUALITY

#else // LIGHTING == 0
	float3 vDiffuse = float3(1.0f, 1.0f, 1.0f);
	float3 vSpecular = float3(0.0f, 0.0f, 0.0f);
#endif
	
	//
	// Fog
	//

#if (FOG_TYPE != 0) // FOG_NONE
	#if (FOG_QUALITY == 1)
	float fFog = In.Last.w;
	#else
	float fFog = FogD(fCamDistance);
	#endif
#else
	float fFog = 0.0f;
#endif

	//
	// Background
	//
	
	float2 vProjTexcoord = In.PositionProj.xy / In.PositionProj.w;
	float4 cBackgroundColor = tex2D(background, vProjTexcoord * (fDistance / fDistanceScale) * vBGScale + vBGOffset);
	
	cBackgroundColor *= float4(vBGColor, In.Color0.a);
	
	//
	// Reflection
	//
	
#if (LIGHTING_QUALITY == 0 || LIGHTING_QUALITY == 1)
	float3 cReflectionColor = texCUBE(cube, reflect(In.PositionOffset, vNormal));
#else
	float3 cReflectionColor = texCUBE(cube, In.Reflect);
#endif

	cReflectionColor *= vReflectColor;

#if (LIGHTING_QUALITY == 2)
	float fFresnel = 0.0f;
	float4 vSpecColor = 0.0f;
	vSpecColor.a = 1.0f;
#else
	float fFresnel = saturate(vReflectFresnel.x + vReflectFresnel.y * pow(saturate(1.0f + dot(-vCamDirection, normalize(In.Normal))), vReflectFresnel.z));
#endif

	//
	// Rim Lighting
	//
	
#if (LIGHTING == 1 && LIGHTING_QUALITY == 0 && defined(RIM_LIGHTING))
	float3 vCamUp = normalize(-cross(mul(vCameraRight, mWorldRotate), vCamDirection));
	float3 vRimColor = clamp(pow((1.0f - dot(vCamDirection, vNormal)) * saturate(dot(vCamUp, vNormal)) * cDiffuseColor.a, 4.0f), 0.0f, vSunColor);
#else
	float3 vRimColor = float3(0.0f, 0.0f, 0.0f);
#endif

	//
	// Final
	//
	
	float3 vFinalColor = lerp((cDiffuseColor.rgb + (cBackgroundColor.rgb * (In.Color0.a - cDiffuseColor.a))) * vDiffuse + vSpecular, cReflectionColor * cDiffuseColor.a + (cBackgroundColor.rgb * ((In.Color0.a - cDiffuseColor.a))), vSpecColor.a * fReflect * fFresnel) + vRimColor;	
		
#if (FOG_OF_WAR == 1)
	vFinalColor *= tex2D(fogofwar, In.Last.xy).a;
#endif

	Out.Color.rgb = lerp(vFinalColor, vFogColor, fFog);
	Out.Color.a = cBackgroundColor.a + cDiffuseColor.a;

	return Out;
}
