// (C)2014 S2 Games
// effect_energy.psh
// 
// An animated shader designed to simulate arcs of electricity/energy
//=============================================================================

//=============================================================================
// Global variables
//=============================================================================
const float fBorderSharpness;
const float fSolidity;

//=============================================================================
// Vertex shader output structure
//=============================================================================
struct VS_OUTPUT
{
	float4 Color0 : COLOR0;
	float4 Texcoord0 : TEXCOORD0;
	float2 Texcoord1 : TEXCOORD1;
};

//=============================================================================
// Pixel shader input structure
//=============================================================================
struct PS_OUTPUT
{
	float4 Color : COLOR;
};

sampler	reveal;

//=============================================================================
// Pixel Shader
//=============================================================================
PS_OUTPUT PS( VS_OUTPUT In )
{
	PS_OUTPUT Out;
	
	float4 cRevealColor = fmod(tex2D(reveal, In.Texcoord0.xy) + tex2D(reveal, (1.0f - In.Texcoord0.zw)), 1.0f);
	
	// Soften the left and right edges of the particle
	float fRevealSoftness = saturate(10.0f * (0.5f - abs(In.Texcoord1.x - 0.5f)));
	
	float fReveal = cRevealColor.a * fRevealSoftness - pow(In.Texcoord1.y, fSolidity);
	
	float4 cDiffuseColor = tex2D(reveal, 1.0f - float2(0.0f, fReveal));
	
	Out.Color.rgb = cDiffuseColor.rgb * In.Color0.rgb;
	Out.Color.a = In.Color0.a * saturate(fReveal * fBorderSharpness);
	
	return Out;
}
